allprojects {
    version = '4.5.4-local'
}

def teamPropsFile(propsFile) {
    def teamPropsDir = rootProject.file('team-props')
    return new File(teamPropsDir, propsFile)
}

buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.1'
        classpath 'com.novoda:bintray-release:0.9'
        classpath 'com.novoda:gradle-static-analysis-plugin:0.8.1'
        classpath 'com.novoda:gradle-build-properties-plugin:0.4.1'
        classpath "com.github.ben-manes:gradle-versions-plugin:0.21.0"
    }
}

subprojects {
    repositories {
        google()
        jcenter()
    }

    apply from: teamPropsFile('static-analysis.gradle')
}

wrapper {
    distributionType Wrapper.DistributionType.ALL
    gradleVersion '5.0'
}

allprojects {
    apply plugin: 'com.github.ben-manes.versions'

    dependencyUpdates.resolutionStrategy {
        componentSelection { rules ->
            rules.all { ComponentSelection selection ->
                boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                    selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
                }
                if (rejected) {
                    selection.reject('Release candidate')
                }
            }
        }
    }
}
