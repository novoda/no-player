apply plugin: 'com.android.library'
apply plugin: 'bintray-release'
apply plugin: 'jacoco'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        minSdkVersion 16
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    lintOptions {
        lintConfig teamPropsFile('static-analysis/lint-config.xml')
        abortOnError true
        warningsAsErrors true
    }
}

dependencies {
    implementation 'com.google.android.exoplayer:exoplayer:2.7.1'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.15.0'
    testImplementation 'org.easytesting:fest-assert-core:2.0M10'
}

project.afterEvaluate({
    task jarTest(type: Jar, dependsOn: assembleDebugUnitTest) {
        from "$project.buildDir/intermediates/classes/test/debug/"
        classifier = 'test'
    }

    configurations {
        testOutput.extendsFrom(testCompile)
    }

    artifacts {
        testOutput jarTest
    }
})

publish {
    userOrg = 'novoda'
    repoName = 'maven'
    groupId = 'com.novoda'
    artifactId = 'no-player'
    version = project.version

    uploadName = 'no-player'
    description = 'player to wrap players'
    website = 'https://github.com/novoda/no-player'
}
